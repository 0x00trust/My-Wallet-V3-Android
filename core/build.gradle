apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.novoda.build-properties'
apply from: '../quality/ktlint.gradle'
apply plugin: 'com.squareup.sqldelight'

buildProperties {
    secrets {
        using(file('../app/secrets.properties')) | file('../app/secretsDefaults.properties')
    }
}

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                events 'skipped', 'failed', 'standardOut', 'standardError'
            }
            reports.junitXml.destination = file('../build/test-results/')
        }
    }

    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        abortOnError true
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
            manifestPlaceholders = [PLACES_KEY: buildProperties.secrets['placesReleaseKey'].string]
        }
        debug {
            manifestPlaceholders = [PLACES_KEY: buildProperties.secrets['placesDebugKey'].string]
        }
    }
}

dependencies {
    implementation project(':wallet')
    implementation project(':sunriver')
    implementation project(':common:network')
    implementation project(':common:interface')
    implementation project(':common-mp:utils')
    implementation project(':veriff')
    implementation project(':blockchainApi')

    api project(':payments')
    api project(':balance')

    // Networking
    implementation Libraries.retrofitJacksonConverter
    implementation Libraries.retrofitRxMoshiConverter
    implementation Libraries.retrofitRxJavaAdapter
    implementation Libraries.okHttpInterceptor
    implementation Libraries.kotlinJson

    implementation Libraries.koin
    implementation Libraries.cardForm
    implementation Libraries.rxRelay

    // Database
    implementation Libraries.sqlDelight
    implementation Libraries.rxSqlDelight

    // Logging
    implementation Libraries.timber

    // Firebase
    implementation platform(Libraries.firebaseBom)
    implementation(Libraries.firebaseConfig) {
        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
    }

    coreLibraryDesugaring Libraries.desugaring

    implementation Libraries.kotlinCoroutines
    implementation Libraries.rxjava3Coroutines

    // Unit Test dependencies
    testImplementation Libraries.junit
    testImplementation Libraries.mockito
    testImplementation Libraries.koinTest
    testImplementation Libraries.hamcrestJunit
    testImplementation Libraries.kotlinJunit
    testImplementation Libraries.kluent
    testImplementation Libraries.jacksonKotlin
    testImplementation(Libraries.mockitoKotlin) {
        ['kotlin-stdlib', 'kotlin-reflect'].each { exclude module: it }
    }
    testImplementation project(':testutils')
    testImplementation project(':testutils-android')
}
